#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Test of shared and contiguous options (--shared and --contiguous).
#          Also uses --batch and --hold options.  Also see test1.53.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
# Note:    This script generates and then deletes files in the working directory 
#          named test1.14.input, test1.14.output, and test1.14.error
#
source ./globals

set test_id              "1.14"
set exit_code            0
set job_id               0
set matches              0

print_header $test_id

#
# Spawn a srun batch job with shared option only
#
spawn $srun --share --hold --batch -t1 $bin_pwd
expect {
	-re "jobid ($number) submitted" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
	}
}

#
# Confirm shared and contiguous flag values
#
if {$job_id != 0} {
	spawn $scontrol show job $job_id
	expect {
		-re "Shared=($number)" {
			set shared_val $expect_out(1,string)
			if {$shared_val == 1} {
				incr matches
			}
			exp_continue
		}
		-re "Contiguous=($number)" {
			set cont_val $expect_out(1,string)
			if {$cont_val == 0} {
				incr matches
			}
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: scontrol not responding\n"
			cancel_job $job_id
			set exit_code   1
		}
		eof {
		}
	}
	cancel_job $job_id
	set job_id 0
} else {
	set exit_code   1
}

#
# Spawn a srun batch job with contiguous option only
#
spawn $srun --contiguous --hold --batch -t1 $bin_pwd
expect {
	-re "jobid ($number) submitted" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		exit 1
	}
	eof {
	}
}

#
# Confirm shared and contiguous flag values
#
if {$job_id != 0} {
	spawn $scontrol show job $job_id
	expect {
		-re "Shared=($number)" {
			set shared_val $expect_out(1,string)
			if {$shared_val == 0} {
				incr matches
			}
			exp_continue
		}
		-re "Contiguous=($number)" {
			set cont_val $expect_out(1,string)
			if {$cont_val == 1} {
				incr matches
			}
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: scontrol not responding\n"
			cancel_job $job_id
			set exit_code 1
		}
		eof {
		}
	}
	cancel_job $job_id
} else {
	set exit_code 1
}

if {$matches != 4} {
	send_user "\nFAILURE: Did not properly set shared and contiguous flags\n"
	set exit_code 1
}
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
