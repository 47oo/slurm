#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Test squeue sorting (--sort option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
source ./globals

set test_id      "5.5"
set exit_code    0
set file_in      "test$test_id.input"
set job_id1      0
set job_id2      0
set job_name1    "job_b"
set job_name2    "job_a"

print_header $test_id

#
# Delete left-over input script
# Build input script file
#
exec $bin_rm -f $file_in
exec echo "#!$bin_bash"          >$file_in
exec echo "$srun $bin_sleep 10" >>$file_in
exec $bin_chmod 700 $file_in

#
# Submit a couple of jobs so we have something to look at
#
spawn $srun --batch --output=/dev/null --error=/dev/null --job-name=$job_name1 -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		exit 1
	}
	eof {
	}
}
if {$job_id1 == 0} {
	send_user "\nFAILURE: srun job submit failure\n"
	kill_srun
	exit 1
}

spawn $srun --batch --output=/dev/null --error=/dev/null --job-name=$job_name2 --hold -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id2 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		exit 1
	}
	eof {
	}
}
if {$job_id2 == 0} {
	send_user "\nFAILURE: srun job submit failure\n"
	kill_srun
	exit 1
}

exec $bin_rm -f $file_in

#
# Check the squeue sort output
#

set found_job1 0
spawn $squeue --sort=j
expect {
	-re " $job_name1 " {
		set found_job1 1
		exp_continue
	}
	-re " $job_name2 " {
		if {$found_job1 != 0} {
			send_user "\nFAILURE: squeue sort error\n"
			set exit_code 1
		}
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: squeue not responding\n"
		set exit_code 1
	}
	eof {
	}
}

cancel_job $job_id2
cancel_job $job_id1
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code

