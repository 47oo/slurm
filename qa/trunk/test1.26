#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Submit job directly to slurmd without use of slurmctld scheduler.
#          (--no-allocate option). NOTE: Needs to run as SlurmUser or root.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
source ./globals

set test_id     "1.26"
set interations 100
set exit_code   0

print_header $test_id

#
# Submit a 1 node job and record the node name
#
set host_0      ""
set timeout $max_job_delay
spawn $srun -N1 -l -t1 $bin_hostname  
expect {
	-re "0: ($alpha_numeric)" {
		set host_0 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
	}
	eof {
		wait
	}
}

#
# Verify node count
#
if {[string compare $host_0 ""] == 0} {
	send_user "\nFAILURE: Did not get hostname of task 0\n"
	exit 1	
}
set include_node $host_0

#
# Submit a job directly to that node
#
set host_1      ""
set slurm_user  1
set timeout     10
spawn $srun -N1 -l --nodelist=$include_node --no-allocate -t1 $bin_hostname  
expect {
	-re "Invalid job credential" {
		send_user "\nWARNING: Not SlurmUser or root.\n"
		set slurm_user 0
		exp_continue
	}
	-re "0: ($alpha_numeric)" {
		set host_1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nWARNING: srun not responding, "
		send_user "expected if not SlurmUser or root.\n"
		set slurm_user 0
	}
	eof {
		wait
	}
}
if {$slurm_user == 0} {
	exit 0;
}
if {[string compare $host_0 $host_1]} {
	send_user "\nFAILURE: host name value wrong\n"
	set exit_code 1
}

#
# Verify node count and name
#
if {[string compare $host_0 ""] == 0} {
	send_user "\nFAILURE: Did not get hostname of task 0\n"
	set exit_code   1	
}
if {[string compare $host_0 $include_node] != 0} {
	send_user "\nFAILURE: Allocation lacked an included node\n"
	set exit_code   1	
}

#
# Run three tasks at a time on some node and do so repeatedly
# This checks for slurmd race conditions
#
set successes 0
set timeout $max_job_delay
for {set inx 0} {$inx < $interations} {incr inx} {
	set failures 0
	spawn $srun -N1 --nodelist=$include_node -t1 -l $bin_hostname
	set   alloc $spawn_id

	spawn $srun -N1 --nodelist=$include_node -Z $bin_usleep 500000
	set   noalloc1 $spawn_id

	spawn $srun -N1 --nodelist=$include_node -Z $bin_usleep 250000
	set   noalloc2 $spawn_id

	set spawn_id $alloc
	expect {
		-i $alloc
		-re "0: ($alpha_numeric)" {
			set host_0 $expect_out(1,string)
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: srun not responding\n"
			kill_srun
			set failures 1
		}
		eof {
			wait
		}
	}

	set spawn_id $noalloc1
	expect {
		-i $noalloc1
		-re "error:" {
			send_user "\nFAILURE: some error happened\n"
			set failures 1
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: srun not responding\n"
			kill_srun
			set failures 1
		}
		eof {
			wait
		}
	}

	set spawn_id $noalloc2
	expect {
		-i $noalloc2
		-re "error:" {
			send_user "\nFAILURE: some error happened\n"
			set failures 1
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: srun not responding\n"
			kill_srun
			set failures 1
		}
		eof {
			wait
		}
	}

	if {$failures == 0} {
		incr successes
		set  include_node $host_0
	} else {
		set exit_code 1
	}
}
if {$successes != $interations} {
	send_user "\nFAILURE: only $successes of $interations completed successfully\n"
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
