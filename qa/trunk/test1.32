#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Test of srun signal forwarding
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
# Note:    This script generates and then deletes files in the working directory
#          named test1.32.prog
#
source ./globals

set test_id     "1.32"
set exit_code   0
set file_prog   "test$test_id.prog"
set matches     0

print_header $test_id

#
# Delete left-over program and rebuild it
#
exec $bin_rm -f $file_prog
exec $bin_make $file_prog
exec $bin_chmod 700 $file_prog

#
# Spawn initial program via srun
#
set timeout $max_job_delay
spawn $srun -N1 -t1 --unbuffered $file_prog
set srun_pid [exp_pid]
expect {
	-re "WAITING" {
		incr matches
		exec $bin_kill -USR1 $srun_pid
		exp_continue
	}
	-re "SIGUSR1" {
		incr matches
		exec $bin_kill -USR2 $srun_pid
		exp_continue
	}
	-re "SIGUSR2" {
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
	}
}
if {$matches != 3} {
	send_user "\nFAILURE: srun failed to forward signals\n"
	set exit_code 1
}

#
# Post-processing
#
if {$exit_code == 0} {
	exec $bin_rm -f $file_prog
	send_user "\nSUCCESS\n"
} else {
	kill_srun
}
exit $exit_code
