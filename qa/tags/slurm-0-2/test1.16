#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Confirm that srun buffering can be disabled (--unbuffered option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
source ./globals

set test_id            "1.16"
set exit_code          0
set job_id             0
set matches            0
set scratch_file       "scratch.$test_id"

print_header $test_id

#
# Create a scratch file
#
exec $bin_touch $scratch_file

#
# Submit a slurm job that will execute 'rm -i'
# The --unbuffered option will send the message which lacks a '\n'
#
set timeout $max_job_delay
spawn $srun --unbuffered --verbose -t1 $bin_rm -i $scratch_file
expect {
	-re "launching ($number).0 on host" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "remove .*\?" {
		set matches 1
		send "y\n"
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
	}
}
exec $bin_rm -f $scratch_file

#
# Confirm message send before '\n' (unbuffered).
#
 
if {$matches != 1} {
	send_user "\nFAILURE: srun --unbuffered option failure\n"
	set exit_code 1
}
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
