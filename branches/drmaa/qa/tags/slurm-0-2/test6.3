#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Test scancel interactive mode (--interactive option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
source ./globals

set test_id      "6.3"
set exit_code    0
set file_in      "test$test_id.input"
set job_id1      0
set matches      0

print_header $test_id

#
# Delete left-over input script
# Build input script file
#
exec $bin_rm -f $file_in
exec echo "#!$bin_bash"          >$file_in
exec echo "$srun $bin_sleep 10" >>$file_in
exec $bin_chmod 700 $file_in

#
# Submit a job so we have something to work with
#
spawn $srun --batch --output=/dev/null --error=/dev/null -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
	}
}
exec $bin_rm -f $file_in

#
# Test interactive scancel
#

spawn $scancel --interactive $job_id1
expect {
	-re "Job $job_id1 not found" {
		send_user "\nFAILURE: Job initiation failure\n"
		set exit_code 1
		exp_continue
	}
	-re "Cancel job.*\? " {
		send "n\n"
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
	}
}
# Quit now if job failed
if {$exit_code != 0} {
	exit $exit_code

}

spawn $scancel --interactive $job_id1
expect {
	-re "Cancel job.*\? " {
		send "y\n"
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
	}
}

spawn $scancel --interactive $job_id1
expect {
	-re "Job $job_id1 not found" {
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
	}
}

if {$matches != 3} {
	send_user "\nFAILURE: scancel interactive option failed\n"
	set exit_code 1
}
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code

