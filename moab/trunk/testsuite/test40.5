#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          psub -r  (job name option)
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "40.5"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set job_id      0
set job_name    "AaBbCcDd"

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in $file_out
make_bash_script $file_in "
  sleep 40
  exit 0
"

#
# Test of psub with name $job_name:
#
set psub_pid [spawn $psub -r $job_name -o $file_out -eo -tW 1 $file_in]  
expect {
	-re "$alpha_numeric.($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: psub not responding\n"
		slow_kill $psub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: psub job submit failure\n"
	exit 1
}

#
# Check Name reported by checkjob
#
set matches 0
set checkjob_pid [spawn $checkjob $job_id]
expect {
	-re "AName: *$job_name" {
		set matches 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: checkjob not responding\n"
		slow_kill $checkjob_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$matches == 0} {
	send_user "\nFAILURE: Name for job not set properly\n"
	set exit_code 1
}
cancel_job $job_id

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in $file_out
}
exit $exit_code
