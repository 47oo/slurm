#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          Eligible start time test: msub -a
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "1.9"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set job_id      0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in $file_out
make_bash_script $file_in "
  pwd
  exit 0
"

#
# Test of msub start time at 2355:
#
set msub_pid [spawn $msub -a 2355 -o $file_out -j oe $file_in]  
expect {
	-re "$alpha_numeric.($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: msub not responding\n"
		slow_kill $msub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: msub job submit failure\n"
	exit 1
}

#
# Check start time reported by checkjob
#
set matches 0
set checkjob_pid [spawn $checkjob $job_id]
expect {
	-re "StartDate: *($number):($number):($number) *($alpha) ($alpha) ($number) 23:55:($number)" {
		set matches 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: checkjob not responding\n"
		slow_kill $checkjob_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$matches == 0} {
	send_user "\nFAILURE: StartDate for job not set properly\n"
	set exit_code 1
}
cancel_job $job_id

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in $file_out
}
exit $exit_code
