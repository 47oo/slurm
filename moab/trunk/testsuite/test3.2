#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          Cancelation of running jobs
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "3.2"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set job_id      0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in $file_out
make_bash_script $file_in "
  sleep 120
  exit 0
"

#
# Submit a job
#
set msub_pid [spawn $msub -o $file_out -j oe -l walltime=100 $file_in]  
expect {
	-re "$alpha_numeric.($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: msub not responding\n"
		slow_kill $msub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: msub job submit failure\n"
	exit 1
}

#
# Wait for job to start then cancel it
#
if {[wait_for_job $job_id RUNNING] != 0} {
	send_user "\nFAILURE: job failed to start running\n"
	cancel_job $job_id
	exit 1
}

set matches 0
set canceljob_pid [spawn $canceljob $job_id]
expect {
	-re "job .* cancelled" {
		set matches 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: checkjob not responding\n"
		slow_kill $checkjob_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$matches == 0} {
	send_user "\nFAILURE: Unable to cancel job\n"
	set exit_code 1
}
if {[wait_for_job $job_id DONE] != 0} {
	send_user "\nFAILURE: job failed to terminate\n"
	cancel_job $job_id
	set exit_code 1
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in $file_out
}
exit $exit_code
