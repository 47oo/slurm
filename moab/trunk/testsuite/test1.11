#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          msub -j (option to merge stdout and stderr)
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "1.11"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set job_id      0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in $file_out
make_bash_script $file_in "
  id
  sleep aaa
  exit 0
"

#
# Test of msub output and error file merge
#
set msub_pid [spawn $msub -o $file_out -j oe $file_in]  
expect {
	-re "$alpha_numeric.($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: msub not responding\n"
		slow_kill $msub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: msub job submit failure\n"
	exit 1
}
if {[wait_for_job $job_id DONE] != 0} {
	send_user "\nFAILURE: error on job\n"
	exit 1
}

#
# Test output file
#
set got_job_grps 0
set got_sleep_err 0
if {[wait_for_file $file_out] == 0} {
	spawn sort $file_out
	expect {
		-re "$sleep_error_message" {
			send_user "\nNo worries, this error is expected\n"
			set got_sleep_err 1
			exp_continue
		}
		-re "(uid=.*\n)" {
			set job_grp_info $expect_out(1,string)
			set got_job_grps 1
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$got_job_grps == 0} {
	send_user "\nFAILURE: msub stdout missing\n"
	set exit_code 1
}
if {$got_sleep_err == 0} {
	send_user "\nFAILURE: msub stderr missing\n"
	set exit_code 1
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in $file_out
}
exit $exit_code
