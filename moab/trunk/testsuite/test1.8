#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          Working directory test: msub -d path
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "1.8"
set exit_code   0
set cwd         [exec pwd]
set file_in     "$cwd/test$test_id.input"
set file_out    "$cwd/test$test_id.output"
set job_id      0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in $file_out
make_bash_script $file_in "
  pwd
  exit 0
"

#
# Test of msub working directoy test
#
set msub_pid [spawn $msub -d /tmp -o $file_out -j oe -l walltime=100 $file_in]  
expect {
	-re "$alpha_numeric.($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: msub not responding\n"
		slow_kill $msub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: msub job submit failure\n"
	exit 1
}
if {[wait_for_job $job_id DONE] != 0} {
	send_user "\nFAILURE: error on job\n"
	cancel_job $job_id
	exit 1
}

#
# Test output file
#
if {[wait_for_file $file_out] == 0} {
	set matches 0
	spawn cat $file_out
	expect {
		-re "/tmp" {
			set matches 1
			exp_continue
		}
		eof {
			wait
		}
	}
	if {$matches == 0} {
		send_user "\nFAILURE: Failed to run in /tmp directory\n"
		set exit_code 1
	}
} else {
	set exit_code 1
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in $file_out
}
exit $exit_code
