#!/usr/bin/expect
############################################################################
# Purpose: Test of Moab functionality
#          pstat H -L (No header and long output format)
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
############################################################################
source ./globals

set test_id     "41.4"
set exit_code   0
set file_in     "test$test_id.input"
set job_id      0


print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec rm -f $file_in
make_bash_script $file_in "
  sleep 40
  exit 0
"

#
# Test of psub bank account
#
set psub_pid [spawn $psub -x -r TEST.$test_id -b dummy_account -o /dev/null -eo -tW 1 $file_in]  
expect {
	-re "($number) submitted" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: psub not responding\n"
		slow_kill $psub_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: psub job submit failure\n"
	exit 1
}

#
# Test account and job name reported by pstat
#
set bank_matches   0
set header_matches 0
set pstat_pid [spawn $pstat -H -L]
expect {
	-re "BANK" {
		incr header_matches
		exp_continue
	}
	-re "dummy_account" {
		incr bank_matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: pstat not responding\n"
		slow_kill $pstat_pid
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if {$bank_matches != 1} {
	send_user "\nFAILURE: psub account missing\n"
	set exit_code 1
}
if {$header_matches != 0} {
	send_user "\nFAILURE: psub header printed missing\n"
	set exit_code 1
}
cancel_job $job_id

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
	exec rm -f $file_in
}
exit $exit_code
